
require:
   fs
   irc
   moment
   .source ->
      StreamSource
   .utilities ->
      is-channel
      get-network as extract-network
      get-channel as extract-channel
   commander
   .log ->
      Logger


method here-command(mthd, cmd, argument, channel, m) =
   @client[mthd](channel, argument)
   @fudge with {
      type = cmd
      channel = channel
      agent = @client.nick
      content = argument
   }

method chanarg-command(mthd, cmd, argument, m) =
   {channel, *text} = argument.split(" ")
   content = text.join(" ")
   @client[mthd](channel, content)
   @fudge with {
      type = cmd
      channel = channel
      agent = @client.nick
      content = content
   }

commands = Object.assign(Object.create(null)) with {

   method say(argument, channel, m) =
      here-command.call(@, .say, .message, argument, channel, m)

   method me(argument, channel, m) =
      here-command.call(@, .action, .action, argument, channel, m)

   method message(argument, _, m) =
      chanarg-command.call(@, .say, .message, argument, m)

   method notice(argument, _, m) =
      chanarg-command.call(@, .notice, .notice, argument, m)

   method action(argument, _, m) =
      chanarg-command.call(@, .action, .action, argument, m)

   method join(argument, _, m) =
      if not is-channel(argument):
         return null
      @client.join(argument)
      null

   method part(argument, channel, m) =
      target = extract-channel(argument or channel)
      if not is-channel(target):
         return null
      @client.part(target)
      null

   method nick(argument, _, m) =
      @client.send(.nick, argument)
      null

   method send(argument, _, m) =
      @client.send(*argument.split(" "))
      null

   method _other(argument, channel, m) =
      @fudge with {
         type = .unknown-command
         channel = m.channel
         agent = @nickname
         command = m.command
         argument = m.argument
      }

}

command-aliases = {
   msg = "message"
   j = "join"
   ns = "message NickServ"
   cs = "message ChanServ"
   broo = "message Broolucks"
}


events = {

   method registered(m) =
      closer(type)() =
         (keys(@client.chans) ++ {""}) each channel ->
            @print-result with {
               type = .quit
               = channel
               agent = @client.nick
               self = true
               connection-event = type
               content = "Disconnected."
            }
      @client.conn.on(.end, closer(.end))
      @client.conn.on(.close, closer(.close))
      @client.say("NickServ", 'id {@nickserv-password}')
      @nickname = @client.nick
      {
         type = .join
         channel = ""
         agent = @client.nick ;; m.args[0]
         self = true
      }

   method motd(text) = {
      type = .motd
      channel = ""
      content = text
   }

   method names(channel, nicks) =
      @users[channel] = new Set(keys(nicks))
      {
         type = .names
         = channel
         content = object with items(nicks) each {nick, level} ->
            {nick, {operator-level = level}}
      }

   method topic(channel, topic, agent, message) = {
      type = .topic
      = channel, = agent
      content = topic
   }

   method join(channel, agent, m) =
      @add-user(channel, agent)
      {
         type = .join
         = channel, = agent
         self = agent === @client.nick
      }

   method part(channel, agent, m) =
      @delete-user(channel, agent)
      {
         type = .part
         = channel, = agent
         self = agent === @client.nick
      }

   method quit(agent, reason, channels, m) =
      @channels-with-user(agent) each channel ->
         @delete-user(channel, agent)
         {
            type = .quit
            = channel, = agent
            content = reason
         }

   method kick(channel, nick, agent, reason, m) =
      @delete-user(channel, nick)
      {
         type = .kick
         = channel, = agent
         kicked = nick
         content = reason
      }

   method kill(agent, reason, channels, m) =
      @channels-with-user(agent) each channel ->
         @delete-user(channel, agent)
         {
            type = .kill
            = channel, = agent
            content = reason
         }

   method message(agent, channel, text, _) =
      m = @fudge with {
         type = .message
         = channel, = agent
         content = text
      }
      if m.channel === m.agent and not @pms.has(m.agent):
         @pms.add(m.agent)
         j = {
            type = .join
            channel = m.agent
            agent = @client.nick
            self = true
         }
         n = {
            type = .names
            channel = m.agent
            content = {
               m.agent => {operator-level = ""}
               @client.nick => {operator-level = ""}
            }
         }
         {j, n, m}
      else:
         m

   ;; method selfMessage(*args) =
   ;;    @unhandled('selfMessage', args)

   method notice(agent, channel, text, m) = @fudge with {
      type = .notice
      = channel, = agent
      content = text
   }

   ;; method ping(*args) =
   ;;    @unhandled('ping', args)

   ;; method pm(*args) =
   ;;    @unhandled('pm', args)

   method ctcp(from, to, text, ctcp-type, m) =
      {cmd >> cmd.to-lower-case(), *args} = text.split(" ")
      {
         type = match ctcp-type:
            .privmsg -> 'ctcp:{cmd}'
            .notice -> 'ctcp-reply:{cmd}'
            else -> 'ctcp-error:{cmd}'
         agent = from
         channel = to
         content = args.join(" ")
      }

   ;; method ctcp-notice(*args) =
   ;;    @unhandled('ctcp-notice', args)

   ;; method ctcp-privmsg(*args) =
   ;;    @unhandled('ctcp-privmsg', args)

   ;; method ctcp-version(*args) =
   ;;    @unhandled('ctcp-version', args)

   method nick(oldnick, newnick, channels, m) =
      if not oldnick or oldnick === @nickname:
         @nickname = newnick
      (@channels-with-user(oldnick) ++ {oldnick, ""}) each channel ->
         @delete-user(channel, oldnick)
         @add-user(channel, newnick)
         {
            type = .nick
            = channel
            from = oldnick
            to = newnick
         }

   method invite(*args) =
      @unhandled('invite', args)

   method "+mode"(channel, by, mode, argument, m) = {
      type = .add-mode
      = channel, agent = by
      = mode
      = argument
   }

   method "-mode"(channel, by, mode, argument, m) = {
      type = .remove-mode
      = channel, agent = by
      = mode
      = argument
   }

   method whois(*args) =
      @unhandled('whois', args)

   method channellist_start(*args) =
      @unhandled('channellist_start', args)

   method channellist_item(*args) =
      @unhandled('channellist_item', args)

   method channellist(*args) =
      @unhandled('channellist', args)

   method raw(*args) =
      pass

   method error(*args) =
      @unhandled('error', args)
}


class IRCClient:

   constructor(@network, @settings) =
      @settings as {
         nickname => @nickname
         => channels
         => user-name
         => real-name
         nickserv-password => @nickserv-password
      }
      @network-prefix = 'irc://{@network}'
      @client = new irc.Client(@network, @nickname) with {
         = user-name
         = real-name
         = channels
      }
      @pms = new Set()
      @users = {=}
      @install-handlers()
      @log = @settings.log

   add-user(channel, name) =
      (@users[channel] or= (new Set())).add(name)

   delete-user(channel, name) =
      (@users[channel] or= (new Set())).delete(name)

   channels-with-user(name) =
      items(@users) each {ch, s} when s.has(name) -> ch

   fudge(m) =
      if not m.agent:
         m.agent = m.channel
         m.channel = ""
      if m.channel === @nickname:
         m.channel = m.agent
      m

   augment(message) =
      message.channel or= ""
      if not message.channel.starts-with(@network-prefix):
         message.channel = '{@network-prefix}/{ch.replace(R.g"/", "\\/")}' where
            ch = message.channel
      message.format = "irc"
      message.time = moment().format("YYYY-MM-DD HH:mm:ss")
      message

   unhandled(m, args) =
      {
         type = .unhandled
         channel = m.channel or ""
         message-type = m
         content = args
      }

   print-result(result) =
      if result:
         (Array! result) each entry ->
            m = @augment(entry)
            @log.log(m)
            print JSON.stringify(m, null, 2)

   install-handlers() =
      items(events) each {k, fn} ->
         @client.add-listener(k) with {*args} ->
            @print-result(fn.call(@, *args))

   process-command(message) =
      channel = extract-channel(message.channel)
      var cmd = message.command.to-lower-case()
      if command-aliases[cmd] as alias:
         {cmd, *args} = alias.split(" ")
         message.command = cmd
         message.argument = (args ++ {message.argument}).join(" ")
      fn = commands[cmd] or commands._other
      result = fn.call(@, message.argument, channel, message)
      @print-result(result)

   disconnect() =
      ;; TODO: log quit message for self
      @client.disconnect()


class Dispatcher:

   constructor(in-stream, @settings) =
      configurations = @settings.networks
      @defaults = @settings.network-defaults
      @in-stream = StreamSource(in-stream, null)
      @networks = {=}
      @configurations = {=}
      @aliases = {=}
      @log = Logger({root = @settings.log-directory})
      items(configurations or {=}) each {alias, cfg} ->
         url = cfg.url or alias
         if url !== alias:
            @aliases[alias] = url
         R"irc://(.*)"! {_, cfg.irc-address} = cfg.url
         @configurations[url] = cfg

   get-configuration(name) =
      if @aliases[name] as real-name:
         return @get-configuration(real-name)
      return @defaults & (@configurations[name] or {=})

   get-network(name) =
      cfg = @get-configuration(name)
      if @networks[cfg.url] as rval:
         return rval
      nw = IRCClient(cfg.irc-address) with {
         nickname = cfg.nicknames[0]
         channels = {}
         user-name = cfg.user-name
         real-name = cfg.real-name
         nickserv-password = cfg.password
         log = @log
      }
      @networks[cfg.url] = nw
      nw

   connect(network, channels = {}) =
      cfg = @get-configuration(network)
      if @networks[cfg.url]:
         throw E.already-connected(network)
      nw = IRCClient(cfg.irc-address) with {
         nickname = cfg.nicknames[0]
         = channels
         user-name = cfg.user-name
         real-name = cfg.real-name
         nickserv-password = cfg.password
         log = @log
      }
      @networks[cfg.url] = nw
      nw      

   async run() =
      while true:
         message = await @in-stream.next()
         if not message:
            console.error with "DONE!"
            items(@networks) each {name, client} ->
               console.error with 'Disconnect from {name}'
               client.disconnect()
            return
         network-name = extract-network(message.channel)
         network = @get-network(network-name)
         network.process-command(message)




async main() =

   cli-options =
      chain commander:
        @version('0.0.1')
        @option('-s, --settings [file]', 'File defining the settings.')
        @parse(process.argv)

   settings = JSON.parse(fs.read-file-sync(cli-options.settings, .utf8))

   d = Dispatcher(process.stdin, settings)

   if settings.join-groups:
      connections = {=}
      items(settings.client.groups) each {name, channels} ->
         channels each channel ->
            ch = extract-channel(channel)
            nw = extract-network(channel)
            if ch !== "":
               (connections[nw] or= {}).push(ch)
      items(connections) each {nw, channels} ->
         d.connect(nw, channels)

   await d.run()
   process.exit(0)

async: main()
