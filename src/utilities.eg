
require:
   lodash
   moment
   md5
   color-space -> lchab

provide:
   is-channel
   compute-level
   get-channel
   get-network
   mom
   shorten
   SortedArray
   find-insert
   string-to-color


color-cache = {=}

string-to-color(s, sat, lig) =
   if color-cache[s] as cached:
      cached
   else:
      hash = md5(s)
      hue = 360 * parse-int(hash[0...2], 16)/256
      c = lchab.rgb({lig, sat, hue}) each v -> Math.floor(v)
      colstring = 'rgb({c[0]}, {c[1]}, {c[2]})'
      color-cache[s] = colstring
      colstring


mom(d) =
   moment(d, "YYYY-MM-DD hh:mm:ss")

shorten(channel) =
   get-channel(channel) or get-network(channel)

get-network(channel) =
   channel.split("/")[... -1].join("/")

get-channel(channel) =
   parts = channel.split("/")
   parts[parts.length - 1]

is-channel(channel) =
   channel.match(R"^[#&~!]")

compute-level(modes) =
   m = lodash.max with modes each
      "q" -> 5
      "a" -> 4
      "o" -> 3
      "h" -> 2
      "v" -> 1
      else -> 0
   m or 0

space-mapping = {
   "0" => "\u2001"
   "1" => "\u2002"
   "2" => "\u2003"
   "3" => "\u2004"
   "4" => "\u2005"
   "5" => "\u2006"
   "6" => "\u2007"
   "7" => "\u2008"
   "8" => "\u2009"
   "9" => "\u200A"
   "A" => "\u200B"
   "B" => "\u200C"
   "C" => "\u200D"
   "D" => "\u202F"
   "E" => "\u205F"
   "F" => "\u3000"
}

reverse-space-mapping = object with
   items(space-mapping) each {k, v} -> {v, k}

space-code(s) =
   (s.to-upper-case() each c -> space-mapping[c] or "?").join("")

parse-space-code(s) =
   var result = ""
   s each c ->
      c2 = reverse-space-mapping[c]
      if c2 === undefined:
         return false
      result += c2
   result


find-insert(xs, x, cmp) =
   var lo = 0
   var hi = xs.length - 1
   while lo <= hi:
      mid = lo + ((hi - lo) // 2)
      match cmp(xs[mid], x):
         < 0  -> lo = mid + 1
         > 0  -> hi = mid - 1
         else -> return {mid, true}
   {lo, false}

class SortedArray:

   constructor(@compare) =
      @_arr = {}

   populate(elements) =
      @_arr = clone(elements).sort(@compare)
      @

   add-all(elements) =
      elements each element ->
         @add(element)
      @

   delete-all(elements) =
      elements each element ->
         @delete(element)
      @

   add(element) =
      last = @_arr[@_arr.length - 1]
      if last === undefined or @compare(last, element) < 0:
         ;; We usually append at the end
         @_arr.push(element)
      else:
         {idx, found} = @_find-index(element)
         if not found:
            @_arr.splice(idx, 0, element)
         else:
            @_arr[idx] = element
      @

   .delete(element) =
      last = @_arr[@_arr.length - 1]
      if element === last:
         @_arr.pop()
      else:
         {idx, found} = @_find-index(element)
         if found:
            @_arr.splice(idx, 1)
         else:
            print @_arr
            print element
            throw E.element-not-found with
               'Element could not be deleted because it could not be found: {element}'
      @

   find-index(element) =
      @_find-index(element)[0]

   _find-index(element) =
      find-insert(@_arr, element, @compare)

   gen [^Symbol.iterator]() =
      @_arr each x -> yield x

