
require:
   .mixins -> MasterEvents
   .channels ->
      ChannelSelector
   .tab ->
      Tab2

require-macros:
   earl-react ->
      %, component

provide:
   ChatApp2


component ChatApp2:

   mixins = {MasterEvents}

   get-initial-state() = {
      views = clone(@props.master.views)
      focus = null
   }

   component-will-mount() =
      if not @state.focus and @state.views.length > 0:
         @props.master.emit(.activate, @state.views[0])

   master-events() = {
      activate(view) =
         @set-state with {focus = view}
      add-view(view) =
         is-first = @state.views.length === 0
         @set-state with {views = @state.views ++ {view}}
         if is-first:
            @props.master.emit(.activate, view)
      remove-view(view) =
         @set-state with {
            tabs = @state.tabs each v when v !== view -> v
         }
   }

   render() =
      .main-window %
         ChannelSelector %
            master = @props.master
         .tab-area %
            @state.views each view ->
               Tab2 %
                  key = view.id
                  active = (@state.focus === view)
                  master = @props.master
                  view = view
