
require:
   .mixins ->
      MasterEvents
      ViewEvents
      ChannelEvents
      PureRender
   ..utilities ->
      shorten
   ..master ->
      View
   .kb ->
      getcode

require-macros:
   earl-react ->
      %, component

provide:
   ChannelSelector
   SubChannelSelector
   ChannelIndicator

globals:
   document


component ChannelSelector:
   mixins = {MasterEvents}

   get-initial-state() = {
      views = consume(@props.master.views)
   }

   master-events() = {
      add-view(view) =
         @set-state with {views = @state.views ++ {view}}
      delete-view(view) =
         @set-state with {views = @state.views each v when v !== view -> v}
   }

   render() =
      .tab-labels %
         @state.views each view ->
            SubChannelSelector %
               key = 'sel-{view.id}'
               master = @props.master
               view = view


component SubChannelSelector:
   mixins = {ViewEvents, MasterEvents}

   get-initial-state() = {
      selected = @props.master.focus === @props.view
      label = @props.view.label
      channels = consume(@props.view.channels)
      focus = @props.view.focus
   }

   master-events() = {
      set-focus(view) =
         @set-state with {selected = view === @props.view}
   }

   view-events() = {
      add-channel(channel) =
         @set-state with {channels = @state.channels ++ {channel}}
      remove-channel(channel) =
         @set-state with {channels = @state.channels each c when c !== channel -> c}
      set-label(old-label, new-label) =
         @set-state with {label = new-label}
      set-focus(old-focus, new-focus) =
         @set-state with {focus = new-focus}
   }

   render() =
      head-channel = if{@state.label, @props.view, @state.focus}
      if not head-channel:
         return span % ""
      if @state.channels.length === 0 and not @props.view.always-show:
         span % ""
      else:
         .tab-group %
            class-name = if{@state.selected, .selected, ""}
            ChannelIndicator %
               class-name = 'tab-label'
               channel = head-channel
               view = @props.view
               master = @props.master
               perma-focus = @state.selected
            @state.channels each ch when ch !== head-channel ->
               ChannelIndicator %
                  key = 'ssel-{ch.id}'
                  class-name = 'tab-channel'
                  channel = ch
                  view = @props.view
                  master = @props.master
                  focus = (ch === @state.focus)


component ChannelIndicator:
   mixins = {ChannelEvents, ViewEvents, MasterEvents}

   get-label() =
      ch = @props.channel
      ch.label or ch.short-name or ch.network

   get-initial-state() = {
      focus = @props.focus
      label = @get-label()
      talking = "not-talking"
      editing = false
      temp-name = ""
   }

   master-events() = {
      set-focus(view) =
         if view === @props.view:
            @set-state with {talking = "not-talking"}
   }

   view-events() = {
      set-focus(_, channel) =
         @set-state with {focus = channel === @props.channel}
      set-label(_, new-label) =
         @set-state with {label = new-label or @get-label()}
   }

   channel-events() = {
      rename(new-name) =
         @set-state with {label = @get-label()}
      add-messages(ms) =
         if ms.length === 0:
            return
         if @props.master.focus === @props.view:
            return
         m = ms[ms.length - 1]
         if m.type === .message:
            @set-state with {talking = .talking}
            set-timeout(___, 1000) with _ ->
               @set-state with {talking = .talked}
   }

   render() =
      is-view = View? @props.channel
      selected = match @props.perma-focus:
         undefined? -> @state.focus
         v -> v
      .channel-indicator %
         class-name = {
            @props.class-name
            if{selected, "active selected", ""}
         }.join(" ")
         .tab-channel-activity %
            class-name = @state.talking
         if is-view and @state.editing:
            then:
               input %
                  ref(elem) =
                     if elem and document.active-element !== elem:
                        elem.focus()
                        elem.set-selection-range(0, elem.value.length)
                  on-blur(e) =
                     @props.view.set-label(@state.temp-name)
                     @set-state with {editing = false, temp-name = ""}
                  on-click(e) =
                     e.stop-propagation()
                  on-key-down(e) =
                     e.stop-propagation()
                     match getcode(e):
                        "Enter" ->
                           @props.view.set-label(@state.temp-name)
                           @set-state with {editing = false, temp-name = ""}
                        "Esc" ->
                           @set-state with {editing = false, temp-name = ""}
                        else ->
                           pass
                  on-change(e) =
                     @set-state with {temp-name = e.target.value}
                  value = @state.temp-name
            else:
               @state.label
         .close-button %
            on-click() =
               if is-view:
                  @props.view.destroy()
               else:
                  @props.view.part(@props.channel)
         on-click() =
            @props.master.set-focus(@props.view)
            if not is-view:
               @props.view.set-focus(@props.channel)
         on-double-click() =
            @set-state with {
               editing = true
               temp-name = @props.view.label or ""
            }

