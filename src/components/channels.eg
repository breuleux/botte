
require:
   .mixins ->
      MasterEvents
      ViewEvents
      ChannelEvents
      PureRender
   ..utilities ->
      shorten
   ..master ->
      View2

require-macros:
   earl-react ->
      %, component

provide:
   ChannelSelector
   SubChannelSelector
   ChannelIndicator


component ChannelSelector:
   mixins = {MasterEvents}

   get-initial-state() = {
      views = consume(@props.master.views)
   }

   master-events() = {
      add-view(view) =
         @set-state with {views = @state.views ++ {view}}
   }

   render() =
      .tab-labels %
         @state.views each view ->
            SubChannelSelector %
               key = 'sel-{view.id}'
               master = @props.master
               view = view


component SubChannelSelector:
   mixins = {ViewEvents, MasterEvents}

   get-initial-state() = {
      selected = @props.master.focus === @props.view
      label = @props.view.label
      channels = consume(@props.view.channels)
      focus = @props.view.focus
   }

   master-events() = {
      set-focus(view) =
         @set-state with {selected = view === @props.view}
   }

   view-events() = {
      add-channel(channel) =
         @set-state with {channels = @state.channels ++ {channel}}
      remove-channel(channel) =
         @set-state with {channels = @state.channels each c when c !== channel -> c}
      set-label(old-label, new-label) =
         @set-state with {label = new-label}
      set-focus(old-focus, new-focus) =
         @set-state with {focus = new-focus}
   }

   render() =
      head-channel = if{@state.label, @props.view, @state.focus}
      if not head-channel:
         return span % ""
      .tab-group %
         class-name = if{@state.selected, .selected, ""}
         ChannelIndicator %
            class-name = 'tab-label'
            channel = head-channel
            view = @props.view
            master = @props.master
            perma-focus = @state.selected
         @state.channels each ch when ch !== head-channel ->
            ChannelIndicator %
               key = 'ssel-{ch.id}'
               class-name = 'tab-channel'
               channel = ch
               view = @props.view
               master = @props.master
               focus = (ch === @state.focus)


component ChannelIndicator:
   mixins = {ChannelEvents, ViewEvents, MasterEvents}

   get-label() =
      ch = @props.channel
      ch.label or ch.short-name or ch.network

   get-initial-state() = {
      focus = @props.focus
      label = @get-label()
      talking = "not-talking"
   }

   master-events() = {
      set-focus(view) =
         if view === @props.view:
            @set-state with {talking = "not-talking"}
   }

   view-events() = {
      set-focus(_, channel) =
         @set-state with {focus = channel === @props.channel}
   }

   channel-events() = {
      rename(new-name) =
         @set-state with {label = @get-label()}
      add-messages(ms) =
         if ms.length === 0:
            return
         if @props.master.focus === @props.view:
            return
         m = ms[ms.length - 1]
         if m.type === .message:
            @set-state with {talking = .talking}
            set-timeout(___, 1000) with _ ->
               @set-state with {talking = .talked}
   }

   render() =
      selected = match @props.perma-focus:
         undefined? -> @state.focus
         v -> v
      div %
         class-name = {
            @props.class-name
            if{selected, "active selected", ""}
         }.join(" ")
         .tab-channel-activity %
            class-name = @state.talking
         @state.label
         on-click() =
            @props.master.set-focus(@props.view)
            if not View2? @props.channel:
               @props.view.set-focus(@props.channel)

