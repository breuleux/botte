
require:
   ..utilities ->
      compute-level
      get-network
      shorten
   lodash
   .links ->
      expand-links

require-macros:
   earl-react ->
      %, component

provide:
   message-renderers

message-renderers = Object.assign(Object.create(null)) with {
   method message(m) =
      @show-message(m.channel, m.agent, m.content)

   method action(m) =
      @show-message(m.channel, m.agent, m.content, .action)

   method "ctcp:action"(m) =
      @show-message(m.channel, m.agent, m.content, .action)

   method traffic(m) =
      @show-message(shorten(m.channel), "", content, .traffic) where
         content =
            items(m.changes) each {nick, changes} ->
               initial = m.initial[nick]
               state = {
                  modes = new Set(initial.modes)
                  present = initial.present
                  movement = {}
                  nick-sequence = {
                     {initial.nickname, compute-level(initial.modes)}
                  }
               }
               changes each
                  #join{} ->
                     state.present = true
                     state.modes = new Set()
                     state.movement.push with
                        span.user-join % "▲"
                  #part{} or #quit{} ->
                     state.present = false
                     state.movement.push with
                        span.user-part % "▼"
                  #kick{} ->
                     state.present = false
                     state.modes = new Set()
                     state.movement.push with
                        span.user-part % "✖"
                  #nick{old, new} ->
                     state.nick-sequence.push with
                        {new, compute-level(state.modes)}
                  #add-mode{mode} ->
                     state.modes.add(mode)
                  #remove-mode{mode} ->
                     state.modes.delete(mode)
                  else ->
                     pass
               if state.nick-sequence.length === 1
                  \ and initial.present === state.present
                  \ and compute-level(state.modes)
                      \ != compute-level(initial.modes):
                     state.nick-sequence.push with
                        {initial.nickname, compute-level(state.modes)}
               state.nick-sequence[state.nick-sequence.length - 1][1] =
                  compute-level(state.modes)
               if state.movement.length > 0 or state.nick-sequence.length > 1:
                  span.user-traffic %
                     span.user-movement %
                        state.movement
                     span %
                        class-name = match state:
                           {movement => {}} ->
                              "user-static"
                           {present => match} ->
                              true? -> "user-joined"
                              false? -> "user-left"
                        state.nick-sequence each {n, l} ->
                           span.traffic-name %
                              span %
                                 class-name = 'user-level-{l}'
                                 n
               else:
                  span % ""

   method topic(m) =
      if m.content:
         @show-message(m.channel, "", ___, .topic) with {
            .channel-topic-content % expand-links.call(@, m.content)
            .channel-topic-set-by % m.agent
         }

   method add-mode(m) =
      ag = m.agent or get-network(m.channel) or "??"
      @show-message(m.channel, "", '{ag} sets mode +{m.mode} {m.argument or ""}')

   method remove-mode(m) =
      ag = m.agent or get-network(m.channel) or "??"
      @show-message(m.channel, "", '{ag} sets mode -{m.mode} {m.argument or ""}')

   method names(m) =
      null

   method notice(m) =
      @show-message(m.channel, m.agent, m.content, .notice)

   method motd(m) =
      @show-message(m.channel, m.agent or "", m.content, .motd)

   method unknown-command(m) =
      @show-message(m.channel, "!ERROR!",
                    \ 'Unknown command: /{m.command}', 'unknown-command')

   method error(m) =
      print m.error
      @show-message(m.channel, "!ERROR!", content, .error) where content = {
         div % m.error.message
         div % stringify-message(m.message)
      }

   method _unhandled(m) =
      @show-message(m.channel, "!UNHANDLED!", stringify-message(m), .unhandled)
}

stringify-message(m) =
   JSON.stringify(m & {_pointers = undefined}, null, 2)
